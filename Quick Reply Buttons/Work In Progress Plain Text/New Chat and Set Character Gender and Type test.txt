/messages names=on 0|
/setvar key=mess {{pipe}}|
/setglobalvar key=run Yes|
/ife ( (have_read_install_instructions != 'Yes') and ( 'Character Maker QR:' in mess)){:
	/buttons labels=["Yes", "No"] Have you read the intallation instructions?|
	/setvar key=selected_btn {{pipe}}|

  /ife ( selected_btn == ''){:
		/echo Aborting | /:"Character Maker V4.Flushvar"|
	:}|
	/setglobalvar key=have_read_install_instructions {{getvar::selected_btn}}
:}|

/ife ( ('Required Downloads' not in mess) and ( 'Character Maker QR:' in mess)){:
	/buttons labels=["Yes","No"] Do you want to delete the current chat?| /setvar key=x {{pipe}}|
	/ife (x == 'Yes' ){:
		/newchat delete=true
	:}|
	/elseif ( x == 'No'){:
		/newchat delete=false
	:}|
	/else {:
		/abort
	:}|
:}|
/elseif ( 'Character Maker QR:' not in mess)){:
	/flushvar mess|
	/abort
:}|

/db-list source=chat field=name |/setvar key=a {{pipe}}|
/foreach {{getvar::a}} {:
	/db-delete source=chat {{var::item}}|
:}|

/buttons labels=["Yes","No"] Do you want to enable debug mode?|
/setvar key=selected_btn {{pipe}}|
/ife ( selected_btn == ''){:
	/echo Aborting | /:"Character Maker V4.Flushvar"|
:}|
/elseif ( selected_btn == 'Yes'){:
	/setvar key=debug Yes|
:}|
/elseif ( selected_btn == 'No'){:
	/setvar key=debug No|
:}|

/buttons labels=["NSFW", "SFW"] What type of character do you want to make?|
/setvar key=selected_btn {{pipe}}|
/ife ( selected_btn == ''){:
	/echo Aborting | /:"Character Maker V4.Flushvar"|
:}|
/else {:
	/setvar key=NSFW {{getvar::selected_btn}}
:}|

/setvar key=tag []|
/setvar key=normal_form None|
/setvar key=transformation []|

/buttons labels=["Female", "Male", "Machine"] What gender is the character you are making? |
/setvar key=sgender {{pipe}}|
/ife ( sgender == ''){:	/:"Character Maker V4.Flushvar"| /echo Aborting |	/abort :}|

/ife ( sgender == 'Machine' ) {:
	/setvar key=stype Machine
:}|
/else {:
	/setvar key=stype "Help me Decide"|
	/getentryfield file="Character Maker Combined NSFW" 29| /setvar key=typex {{pipe}}|
	/while left=stype right="Help me Decide" {:
		/buttons labels=["Help me Decide", "Human", "Anthro", "Demi-Human", "Beastkin", "Non-Humanoid", "Pokémon", "Machine"] What type of character are you making? |
		/setvar key=stype {{pipe}}|
		/ife ( stype == ''){:
			/echo Aborting | /:"Character Maker V4.Flushvar"|
		:}|
		/ife ( stype == 'Help me Decide' ){:
			/input rows=8 What race do you want the character to be?|
			/setvar key=inp {{pipe}}|
			/genraw as=char Respond to the question: What type of character is a {{getvar::inp}} and can it transform?
The reply should be in this format:
<div>{{getvar::inp}} is a x</div>
<div>Transformation: y</div>
x is one of the following "Human", "Anthro", "Demi-Human", "Beastkin", "Non-Humanoid", "Pokémon", "Machine"
y is Yes or No depending if the race can transform(Werewolves can transform into humans, Vampires can transform into bats etc...)
INFORMATION: {{getvar::typex}}
INSTRUCTION: Only respond in the given format include <div> and </div>.|

			/setvar key=stype {{pipe}}|
			/buttons labels=["Continue"]{{getvar::stype}}|
			/setvar key=stype {{pipe}}|
			/ife ( stype == '' ){:
				/echo Aborting | /:"Character Maker V4.Flushvar"|
			:}|
			/elseif ( stype == 'Continue' ){:
				/setvar key=stype "Help me Decide"|
			:}|
		:}|
		/else {:
			/ife ( stype == 'Anthro'){:
				/setvar key=s2 AN|
			:}|
			/elseif ( stype == 'Demi-Human'){:
				/setvar key=s2 DHN|
			:}|
			/elseif ( stype == 'Beastkin'){:
				/setvar key=s2 BN|
			:}|
			/elseif ( stype == 'Non-Humanoid'){:
				/setvar key=s2 NHN|
			:}|
			/elseif ( stype == 'Pokémon'){:
				/setvar key=s2 PN|
			:}|
		:}|
	:}|
:}|



/setvar key=species ["Canine", "Equine", "Feline", "Reptilian", "Aviary", "Leporidae(Rabbit)", "Other"]|
/setvar key=type None|
/setvar key=sa "Leporidae(Rabbit)"|
/ife ( stype != 'Human'){:
	/buttons labels=species What type of species should the character be? |
	/setvar key=s {{pipe}}|
	/ife ( s == ''){:
		/echo Aborting | /:"Character Maker V4.Flushvar"|
	:}|
	/elseif ( s == 'Other'){:
		/input rows=8 Write what kind of species the character should be.|
		/setvar key=s {{pipe}}|
		/ife ( s == ''){:
			/echo Aborting | /:"Character Maker V4.Flushvar"|
		:}|
	:}|
	/elseif ( s == sa){:
		/setvar key=s Leporidae|
	:}|
	/setvar key=type {{getvar::s}}|

	/let uppercase {: string=
	    /re-exec find=/(.)/g "abcdefghijklmnopqrstuvwxyz" | /map {{pipe}} {: /getat index="1" {{var::item}} :} | /let key=mfrom ||
	    /re-exec find=/(.)/g "ABCDEFGHIJKLMNOPQRSTUVWXYZ" | /map {{pipe}} {: /getat index="1" {{var::item}} :} | /let key=mto ||
	    /foreach {{var::mfrom}} {:
	        /getat index={{var::index}} {{var::mto}}
	        | /let lto {{pipe}}
	        | /re-replace find=/{{var::item}}/g replace={{var::lto}} {{var::string}}
	        | /var string {{pipe}}
	    :}
	:} ||
	/:uppercase string="{{getvar::s}}" |
	/setvar key=s {{pipe}}|
:}|
/else {:
	/setvar key=type Human|
	/setvar key=s HN|
:}|

/addvar key=tag {{getvar::NSFW}}|

/ife ( sgender == 'Female'){:
	/ife ( s != 'HN'){:
		/addvar key=tag "F{{getvar::s2}}"|
	:}|
	/addvar key=tag "F{{getvar::s}}"|
:}|
/elseif ( sgender == 'Male'){:
	/ife ( s != 'HN'){:
		/addvar key=tag "M{{getvar::s2}}"|
	:}|
	/addvar key=tag "M{{getvar::s}}"|
:}|
/else {:
	/addvar key=tag (WIP)|
:}|

/buttons labels=["Yes", "No"] Can the character transform between a Human form and Another Form?| /setvar key=tr {{pipe}} |
/ife ( tr == '') {:/echo Aborting | /:"Character Maker V4.Flushvar"| :}|

/ife ( tr == 'Yes' ){:
	/addvar key=tag TRANSFORM|
	/input default=Human rows=8 <div>What are the characters main form?</div><div>Example: A werewolf's main form could be Human or Werewolf</div>|
	/setvar key=normal_form {{pipe}}|
	/ife ( normal_form == ''){:
		/echo Aborting | /:"Character Maker V4.Flushvar"|
	:}|
	/elseif ( normal_form != 'Human'){:
		/buttons labels={{getvar::species}} Select the form type of the character.|
		/addvar key=normal_form "({{pipe}})"|
	:}|
	/setvar key=w Add|
	/while left=w right=Add rule=eq {:
		/input rows=8 What are the character's transformed form?|
		/setvar key=input {{pipe}}|
		/ife ( input == ''){:
			/echo Aborting | /:"Character Maker V4.Flushvar"|
		:}|
		/elseif ( input != 'Human'){:
			/buttons labels={{getvar::species}} Select the form type of the character.|
			/addvar key=transformation "{{getvar::input}}({{pipe}})"|
		:}|
		/else {:
			/addvar key=transformation {{getvar::input}}|
		:}|
		/buttons labels=["Yes", "No"] Are you done with the transformations?|
		/setvar key=selected_btn {{pipe}}|
		/ife ( selected_btn == ''){:
			/echo Aborting | /:"Character Maker V4.Flushvar"|
		:}|
		/elseif ( selected_btn == 'Yes'){:
			/setvar key=w {{getvar::selected_btn}}|
		:}|
	:}|
:}|

/:"Character Maker V4.Is real"|

/setvar key=tag1 {{noop}}|
/foreach {{getvar::tag}} {:
	/setvar key=it {{var::item}}|
	/setvar key=in {{var::index}}|
	/ife ( in == 0){:
		/setvar key=tag1 {{getvar::it}}|
	:}|
	/else {:
		/addvar key=tag1 ", {{getvar::it}}"|
	:}|
:}|

/setvar key=tran1 None|
/foreach {{getvar::transformation}} {:
	/setvar key=it {{var::item}}|
	/setvar key=in {{var::index}}|
	/ife ( in == 0){:
		/setvar key=tran1 {{getvar::it}}|
	:}|
	/else {:
		/addvar key=tran1 ", {{getvar::it}}"|
	:}|
:}|

/db-add source=chat name=normal_form {{getvar::normal_form}}|
/db-disable source=chat normal_form|
/db-add source=chat name=transformation {{getvar::tran1}}|
/db-disable source=chat transformation|
/db-add source=chat name=tags {{getvar::tag1}}|
/db-disable source=chat tags|
/db-add source=chat name=type {{getvar::type}}|
/db-disable source=chat type|
/db-add source=chat name=gender {{getvar::sgender}}|
/db-disable source=chat gender|
/flushglobalvar run|
/:"Character Maker V4.01. Generate Basic Information (form)"|
/:"Character Maker V4.Flushvar"|
